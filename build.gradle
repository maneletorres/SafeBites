// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        composeCompilerVersion = '1.3.2'
    }

    dependencies {
        classpath libs.com.google.dagger.hilt.android.gradle.plugin
        classpath libs.androidx.navigation.safe.args.gradle.plugin
    }
}

plugins {
    alias libs.plugins.com.android.application apply false
    alias libs.plugins.com.android.library apply false
    alias libs.plugins.org.jetbrains.kotlin.android apply false
    alias libs.plugins.com.github.ben.manes.versions
    alias libs.plugins.nl.littlerobots.version.catalog.update
}

// region Gradle versions plugin configuration:
// https://github.com/ben-manes/gradle-versions-plugin
versionCatalogUpdate {
    sortByKey = false
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}
// endregion